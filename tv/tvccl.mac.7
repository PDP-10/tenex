
search sysdef
ifn ft10x,<search stenex>
ifn ft20,<search monsym>
external rpgSw		; set this switch if called by other program
internal eVec1		; RE-ENTER location in entry vector
internal edChan		; is used for passing JFN to TV

sail==400010		; SAIL entry location


	printx	Run once before SSaving....

    
init:	RESET			; clear all lower forks

	movei	1, 400000	; current fork handle
	move	2, [xwd 3,eVec]	; set entry vector
	SEVEC

	hrroi	1, [asciz "
  Entry Vectors set now.  Ok to SSave
"]
	PSOUT

	HALTF			; done with init can do share save now
	jrst	.-1		; done allow continue


eVec:	jrst	start		; start location
eVec1:	jrst	reEnt		; reentry loc, changed to JRST REENTER
				; .. by TV init code
	jrst	edtCCL


reEnt:	setom	rpgSw
start:  setzm	edChan		; no JFN to pass
ifn ft20,<move 1, ['TVEDIT']>
ifn ft10x,<move 1, ['TV']>	;name for SYSTAT
	setnm			; Set it.
	jrst	sail		; start up SAIL

edtCCL:	movem	1, edChan	; pass JFN in edit-chan to TV
	jrst	sail


edChan:	oct	0		; will contain JFN to be passed to TV

	end	init
