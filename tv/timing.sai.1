Begin "timing"
   Define ! = "Comment", ESC = '33, DEL = '177;
   
   !	Collect string from keyboard and spit it back out.
	This way hope to discover how long various functions take
	  to execute by various display terminals.
	There is a brief exec language.  The exec command prefix
	  is escape.  The commands are:  

	       ESC  pass ESC
		^   pass ^
		O   display old string
		N   display new string
		S   swap old and new
		J   join new and old
		D   rub first charaacter
	       DEL  rub last char of new string
		X   kill new string
		+   append to new string
		-   don't

	  ^  prefix is used to convert to and pass control characters

     Pentti Kanerva, 2-April-79
   ;

String OLDSTR, NEWSTR;
Integer GETRET, CH;
Boolean GATHER;

Integer Simple Procedure GET;
 While True Do
  If (ch _ INCHRW Land '177) = "^" Then Return (INCHRW Land '37) ! ^X;
  Else If ch Neq esc Then Return (ch)				 ! X;
  Else If (ch _ INCHRW Land '177) = esc Or ch = "^"
   Then Return (ch)					       ! $$, $^;
  Else
   Begin "$X"
      gather _ False;
      If "a" Leq ch Leq "z" Then ch _ ch - '40;  ! Uppercase;

      If      ch = "O" Then outStr (oldStr)
      Else If ch = "N" Then Begin outStr(newStr); gather _ True End
      Else If ch = "S" Then newStr Swap oldStr
      Else If ch = "J" Then newStr _ newStr & oldStr
      Else If ch = "D" Then newStr _ newStr [2 to Inf]
      Else If ch = DEL Then newStr _ newStr [1 to Inf-1]
      Else If ch = "X" Then newStr _ ""
      Else If ch = "+" Then gather _ True
      Else If ch = "-" Then gather _ False
      Else outChr ('7)  ! Ring;
      ;
   End "$X"
;

			! +  +  +  + ;
    outStr ("
		-- Store-N-Print --

""Commands"" are:  ^&char or ESC & one of ESC,^,O,N,S,J,D,DEL,X,+,-

");

    ! SET BINARY MODE;
    sFMod ('777777, rFMod('777777) Land '777477);

    gather _ False;  

    While True Do
     Begin Integer c;
	outChr (c _ get);
	If gather Then newStr _ newStr & c;
     End
    ;
End "timing"
;
