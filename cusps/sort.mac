TITLE	SORT	VERSION 6A		
SUBTTL	STAND-ALONE SORT		AL BLACKINGTON/CAM

EDIT==3		;EDIT NUMBER
VERSION==601	;VERSION NUMBER

;COPYRIGHT 1970,1971,1972, DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.

	TWOSEG
	LOC	137
	XWD	VERSION,EDIT
	RELOC	400000


;SORT ROUTINES USED:

	EXTERNAL PSORT.,MERGE.,KEY.,RELES.,RETRN.,ENDS.

;I/O ROUTINES USED:

	EXTERNAL OPEN.,CLOSE.,READ.,WRITE.,WADV.,PURGE.

;MISCELLANEOUS ROUTINES USED

	EXTERNAL GD6.,GD7.,MAG.,ULOSE.,RESET.,UUO.

	OPDEF	UGD6	[17B8]		;GD6.
	OPDEF	UGD7	[20B8]		;GD7.
	OPDEF	UMAG	[22B8]		;MAG.
	OPDEF	UOPENI	[1B8+4B12]	;OPEN. FOR INPUT
	OPDEF	UOPENO	[1B8+10B12]	;OPEN. FOR OUTPUT
	OPDEF	UREAD	[2B8+2B12]	;READ.
	OPDEF	UWRITE	[2B8+3B12]	;WRITE.
	OPDEF	UWADV	[2B8+4B12]	;WADV.
	OPDEF	UCLOSE	[1B8+1B12]	;CLOSE.

;MINIMUM SIZE OF CORE WE WILL USE WHEN RUNNING (LW-SEG + HI-SEG)

	RUNSIZ==^D10	;10K CORE

FIXNUM==:^D16		;DEFINED TO PREVENT UNDEF GLOBAL
			;OTHERWISE NEVER USED BY SORT
;UUO DISPATCH TABLE

INTERNAL UUO1.,UUO2.,UUO3.,UUO4.,UUO5.,UUO6.

	DEFINE DISPAT,<
	XLIST
UUO1.:	XWD	OPEN.,CLOSE.
UUO2.:	XWD	ULOSE.,ULOSE.
	XWD	READ.,WRITE.
	XWD	WADV.,ULOSE.
	XWD	ULOSE.,ULOSE.
	XWD	PURGE.,ULOSE.
	XWD	ULOSE.,ULOSE.
	XWD	ULOSE.,ULOSE.
	XWD	ULOSE.,ULOSE.
UUO3.:	REPEAT ^D8,<XWD ULOSE.,ULOSE.>
UUO4.:	REPEAT ^D8,<XWD ULOSE.,ULOSE.>
UUO5.:	REPEAT ^D8,<XWD ULOSE.,ULOSE.>
UUO6.:	REPEAT ^D9,<EXP ULOSE.>
	EXP	GD6.
	EXP	GD7.
	EXP	ULOSE.
	EXP	MAG.
	REPEAT ^D13,<EXP ULOSE.>
	LIST
	>

	DISPAT;


PPSIZ.==:40		;SIZE OF PUSH-DOWN LIST


START:	JRST	TTYCOM		;NORMAL START
	JRST	CCLCOM		;START FOR CCL

START0:	MOVE	TA,JOBFF	;SAVE NEW
	MOVEM	TA,TOPCOM	;  JOBFF

START1:	ANDI	SW,COMTTY+COMEOF	;CLEAR MOST FLAGS

START2:	MOVE	TA,TOPCOM
	MOVEM	TA,JOBFF

	TRNN	SW,COMTTY	;COMMANDS FROM TTY?
	TTCALL	3,[ASCIZ "
*"]
	TRNE	SW,COMEOF	;ANY MORE COMMAND FILE?
	JRST	START		;NO
	PUSHJ	PP,GETKAR	;GET A COMMAND CHARACTER
	CAIN	CH,15		;IF <C.R.>,
	JRST	START2		;  LOOP

	IORI	SW,REGET	;SET 'REGET LAST CHARACTER'
	MOVE	TA,[XWD LOLOW,LOLOW+1]; CLEAR SOME IMPURE AREA
	SETZM	LOLOW
	BLT	TA,HILOW
	MOVE	TA,[XWD SRPAR,SRTDEF]
	BLT	TA,STDEVS+MAXDEV-1
	MOVEI	KP,KEYDEF	;MOVE KEY POINTER TO START OF TABLE

	MOVEI	DA,OUTDEF	;SCAN OUTPUT
	PUSHJ	PP,COMSTR	;  COMMAND STRING
	CAIE	CH,"="		;ILLEGAL IF TERMINATED WITH
	JRST	COMS9		;  OTHER THAN '_' OR '='

	MOVEI	DA,INDEF	;SCAN INPUT
	PUSHJ	PP,COMSTR	;  COMMAND STRING
	CAIE	CH,15		;ILLEGAL IF TERMINATED WITH
	JRST	COMS9		;  OTHER THAN <C.R.>
;MAKE SURE WE HAVE A RECORD SIZE

	SKIPE	RECSIZ		;ANY SIZE?
	JRST	START4		;YES--OK

	TRNN	SW,COMTTY	;IF COMMANDS FROM TTY,
	JRST	START3		;  ASK FOR RECORD SIZE, ELSE
	TTCALL	3,[ASCIZ "?NO RECORD SIZE
"]
	JRST	RESTRT

START3:	TTCALL	3,[ASCIZ "RECORD SIZE: "]
	PUSHJ	PP,GETDEC
	JUMPE	TA,START3
	MOVEM	TA,RECSIZ

	CAIN	CH,15		;WAS TERMINATOR <C.R.>?
	JRST	START4		;YES

	PUSHJ	PP,SKPCR	;NO--SKIP TO END OF COMMAND LINE
	JRST	START3

;CHECK INPUT FILE PARAMETERS

START4:	PUSHJ	PP,SCANFI

;CHECK OUTPUT FILE PARAMETERS

	PUSHJ	PP,SCANFO

;CHECK KEYS

	PUSHJ	PP,SCNKEY

	TRNE	SW,COMERA	;ANY ERRORS?
	JRST	RESTRT		;YES--QUIT
;SET UP RECORD AREAS

	MOVSI	TB,(POINT 7,0)	;ASSUME ASCII
	MOVE	TA,RECMOD+INDEF	;IS INPUT SIXBIT?
	CAIN	TA,SIXMOD
	MOVSI	TB,(POINT 6,0)	;YES--THEN SO IS SORT RECORD
	MOVE	TA,RECMOD+OUTDEF;IS OUTPUT SIXBIT?
	CAIN	TA,SIXMOD
	MOVSI	TB,(POINT 6,0)	;YES--THEN SO IS SORT RECORD

	HRR	TB,JOBFF
	MOVEM	TB,RECLOC

	MOVE	TA,RECSIZ
	MOVEI	TC,5
	TLNN	TB,100
	MOVEI	TC,6
	IDIVI	TA,(TC)
	SKIPE	TA+1
	ADDI	TA,1
	MOVEM	TA,RECWDS

	CAMGE	TA,MAXLBL	;BIGGER THAN LARGEST LABEL?
	MOVE	TA,MAXLBL	;NO--USE THAT SIZE
	CAIGE	TA,^D18		;BIGGER THAN STANDARD LABEL?
	MOVEI	TA,^D18		;NO--USE THAT SIZE

	PUSHJ	PP,SETJFF	;BUMP JOBFF

;SET UP WRITE FOR OUTPUT FILE

	MOVE	TA,RECSIZ
	DPB	TA,[POINT 12,WRITE+1,11]
	MOVEI	TA,(UWRITE)
	MOVE	TB,OUTDEF+RECMOD
	CAIE	TB,SIXMOD
	MOVEI	TA,(UWADV)
	HRLM	TA,WRITE
;SET ASIDE AREA FOR KEYS

	MOVE	TA,JOBFF
	MOVSM	TA,CALLST+2
	EXCH	TA,KEYWRK
	HRLM	TA,CALLST+1
	PUSHJ	PP,SETJFF

;SET UP FILE TABLES

	PUSHJ	PP,SETFT

;BUILD KEYS

	MOVE	TA,JOBFF
	HRRM	TA,CALLST+2
	PUSHJ	PP,KEYBLD

	TRNN	SW,COMTTY	;INPUT FROM TTY?
	RELEASE	COM,		;YES--RELEASE IT

;SET MEMORY SIZE

SETMEM:	MOVEI	TA,RUNSIZ*2000-1	;MINIMUM JOBREL
	MOVEI	TB,.		;ARE WE
	TRNN	TB,1B18		;  IN TWO-SEGMENT PROGRAM?
	JRST	STMEM1		;NO

	HRRZ	TB,JOBHRL	;YES--DEDUCT
	SUBI	TA,1(TB)	;  SIZE OF HI-SEG
STMEM1:	HRRZ	TB,JOBREL	;IF SIZE OF
	CAMG	TA,TB		;  LOW-SEG IS ALREADY LARGER THAN THAT
	MOVEI	TA,(TB)		;  USE THE LARGER SIZE
	HRRZM	TA,MEMRY.##

;CBLIO CALLS 'PUSHJ 17,@TRAC1.' WHEN TROUBLE OCCURS

	MOVSI	TA,(POPJ 17,)
	MOVEM	TRAC1.##
SUBTTL THE SORT PROCESS

	PUSH	PP,SW		;SAVE SWITCHES
	PUSHJ	PP,CALLST	;CALL PSORT.

INPROC:	UOPENI	INFTAB		;OPEN INPUT
	SETZM	ICOUNT		;CLEAR INPUT RECORD COUNTER

IPROC1:	UREAD	INFTAB		;READ INPUT
	AOSA	ICOUNT		;NOT AT END: BUMP COUNT
	JRST	IPROC4		;AT END

	MOVE	16,RECWDS	;RELEASE THAT
	PUSHJ	17,RELES.	;  RECORD
	JRST	IPROC1		;LOOP

IPROC4:	UCLOSE	INFTAB		;CLOSE INPUT

	PUSHJ	17,MERGE.	;MERGE SCRATCH FILES

OTPROC:	UOPENO	OTFTAB		;OPEN OUTPUT
	SETZM	OCOUNT		;CLEAR OUTPUT RECORD COUNTER

OPROC1:	PUSHJ	17,RETRN.	;RETURN A RECORD
	AOSA	OCOUNT		;NOT AT END: BUMP COUNTER
	JRST	OPROC4		;AT END

	PUSHJ	PP,WRITE	;WRITE THE RECORD
	JRST	OPROC1		;LOOP

OPROC4:	UCLOSE	OTFTAB		;CLOSE OUTPUT

	PUSHJ	17,ENDS.	;CLEAN UP

	MOVE	TA,ICOUNT	;BE SURE
	CAME	TA,OCOUNT	;  COUNTS AGREE
	JRST	BADCNT		;OOPS!!
	TTCALL	3,[ASCIZ "SORTED "]
	PUSHJ	PP,TYPDEC
	TTCALL	3,[ASCIZ " RECORDS
"]

OPROC5:	POP	PP,SW		;GET SWITCH REGISTER BACK
	TRNN	SW,COMEOF	;IF NOT END-OF-COMMAND-FILE,
	JRST	REGO		;  DO ANOTHER SORT

	CALLI	$RESET		;GO BACK TO
	CALLI	1,$EXIT		;  MONITOR
SUBTTL SCAN COMMAND FOR ONE DESCRIPTOR

COMSTR:	SETZM	0(DA)		;CLEAR
	MOVEI	TA,1(DA)	;  CURRENT
	HRLI	TA,0(DA)	;  ENTRY
	BLT	TA,COMSIZ-1(DA)
	SETOM	BLOCKF(DA)
	SETOM	RECMOD(DA)
	SETOM	LABEL(DA)

	MOVEI	DP,DEVICE-1(DA)	;SET DEVICE POINTER
	HRLI	DP,-MAXDEV

COMS0:	PUSHJ	PP,GETNAM	;GET FIRST NAME
	CAIN	CH,":"		;WAS IT A DEVICE?
	JRST	COMS10		;YES
COMS1:	MOVEM	TA,FILNAM(DA)	;STASH FILE NAME
	CAIN	CH,"."		;WAS IT FILE-NAME?
	JRST	COMS11
COMS2:	CAIN	CH,"["		;PROJ-PROG NUMBER COMING UP?
	JRST	COMS13
COMS3:	CAIN	CH,"/"		;SWITCH COMING UP?
	JRST	COMS12
	CAIN	CH,"@"		;INDIRECT COMMANDS?
	JRST	INDCOM
	CAIE	CH,"="		;WAS IT A
	CAIN	CH,15		;  LEGAL TERMINATOR?
	POPJ	PP,		;YES

COMS9:	TTCALL	3,[ASCIZ "
?IMPROPER SORT COMMAND"]
	JRST	RESTRT
;":" SEEN -- STASH DEVICE

COMS10:	JUMPE	TA,COMS9	;NULL ILLEGAL
	AOBJP	DP,MORDEV	;ANY ROOM FOR ANOTHER DEVICE?
	MOVEM	TA,(DP)		;YES--STASH IT
	AOS	NUMDEV(DA)	;UP COUNT OF DEVICES
	JRST	COMS0		;LOOP

;"." SEEN -- GET EXTENSION

COMS11:	PUSHJ	PP,GETNAM	;GET EXTENSION
	HLLZM	TA,FILEXT(DA)

	MOVEI	TA,0		;CLEAR TA
	JRST	COMS2		;LOOP

;"/" SEEN -- PROCESS SITCH

COMS12:	PUSHJ	PP,GETKAR	;GET SWITCH LETTER
	CAIN	CH,"A"
	JRST	ASWICH
	CAIN	CH,"B"
	JRST	BSWICH
	CAIN	CH,"K"
	JRST	KSWICH
	CAIN	CH,"L"
	JRST	LSWICH
	CAIN	CH,"R"
	JRST	RSWICH
	CAIN	CH,"S"
	JRST	SSWICH
	CAIN	CH,"T"
	JRST	TSWICH

	TTCALL	3,[ASCIZ "
?'"]
	TTCALL	1,CH
	TTCALL	3,[ASCIZ "' IS NOT A VALID SWITCH"]
	JRST	RESTRT

;GET PROJECT-PROGRAMMER NUMBER

COMS13:	PUSHJ	PP,GETOCT	;GET PROJECT NUMBER
	CAIE	CH,","		;IF NOT COMMA TERMINATOR,
	JRST	COM13A		;  ERROR
	HRLZM	TA,PPN(DA)	;STASH PROJECT NUMBER
	PUSHJ	PP,GETOCT	;GET PROGRAMMER NUMBER
	CAIE	CH,"]"		;IF NOT RIGHT-BRACKET TERMINATOR,
	JRST	COM13A		;  ERROR
	HRRM	TA,PPN(DA)	;STASH PROGRAMMER NUMBER

	PUSHJ	PP,GETKAR	;GET NEXT COMMAND CHARACTER
	JRST	COMS3

COM13A:	TTCALL	3,[ASCIZ "?INVALID PROJ-PROG CONSTRUCTION
"]
	JRST	RESTRT
;TOO MANY DEVICES

MORDEV:	TTCALL	3,[ASCIZ "
?TOO MANY DEVICES FOR "]
	MOVEI	TA,[ASCIZ "INPUT"]
	CAIE	DA,INDEF
	MOVEI	TA,[ASCIZ "OUTPUT"]
	TTCALL	3,(TA)


;RESTART AFTER COMMAND ERROR

RESTRT:	PUSHJ	PP,SKPCR

REGO:	TRNE	SW,COMTTY
	TRZE	SW,COMEOF
	JRST	START
	JRST	START1

;SKIP THRU COMMAND UNTIL <C.R.> SEEN

SKPCR:	SKIPA	CH,COMKAR
	PUSHJ	PP,GETKAR
	CAIE	CH,15
	JRST	.-2
	POPJ	PP,
;"A" SWITCH
;SET ASCII MODE

ASWICH:	MOVEI	TA,ASCMOD

ASWCH1:	SKIPL	RECMOD(DA)	;IS MODE ALREADY SET?
	JRST	ASWCH2		;YES--ERROR MAYBE
	MOVEM	TA,RECMOD(DA)	;NO--STASH THIS MODE
	JRST	ENDSW		;LEAVE

ASWCH2:	CAMN	TA,RECMOD(DA)	;IS NEW MODE EQUAL TO PREVIOUS MODE?
	JRST	ENDSW		;YES--OK
	TTCALL	3,[ASCIZ "
?CONFLICT BETWEEN /A AND /S"]
	JRST	RESTRT

;"S" SWITCH
;SET SIXBIT MODE

SSWICH:	MOVEI	TA,SIXMOD
	JRST	ASWCH1

;"B" SWITCH
;GET BLOCKING FACTOR

BSWICH:	PUSHJ	PP,GETDEC	;GET DECIMAL NUMBER
	SKIPL	BLOCKF(DA)	;ANY BLOCKING FACTOR SEEN BEFORE?
	JRST	BSWCH1		;YES--ERROR MAYBE
	MOVEM	TA,BLOCKF(DA)	;NO--STASH THIS ONE
	JRST	ENDSW1		;LEAVE

BSWCH1:	CAMN	TA,BLOCKF(DA)	;SAME BLOCKING FACTOR AS PREVIOUSLY?
	JRST	ENDSW1		;YES--OK
	TTCALL	3,[ASCIZ "
?ONLY ONE /B ALLOWED PER FILE"]
	JRST	RESTRT
;"L" SWITCH
;SET LABEL CODE

LSWICH:	PUSHJ	PP,GETKAR	;GET NEXT CHARACTER
	MOVNI	TA,1
	CAIN	CH,"S"
	MOVEI	TA,LABELS
	CAIN	CH,"N"
	MOVEI	TA,LABELN
	CAIN	CH,"O"
	MOVEI	TA,LABELO
	JUMPL	TA,LSWCH2

	SKIPL	LABEL(DA)	;LABELS ALREADY SPECIFIED?
	JRST	LSWCH1		;YES--ERROR MAYBE
	MOVEM	TA,LABEL(DA)	;NO--STASH THIS ONE

LSWCH0:	CAIE	TA,LABELN	;NON-STANDARD LABELS?
	JRST	ENDSW		;NO--DONE
	PUSHJ	PP,GETDEC	;YES--GET LABEL SIZE
	JUMPE	TA,LSWCH2	;IF NONE--ERROR
	SKIPE	LABSIZ(DA)	;ALREADY SEEN ONE?
	JRST	LSWCH3		;YES--ERROR MAYBE
	MOVEM	TA,LABSIZ(DA)	;NO--STASH THIS ONE
	JRST	ENDSW1

LSWCH1:	CAMN	TA,LABEL(DA)	;DOES THIS CODE MATCH PREVIOUS ONE?
	JRST	LSWCH0		;YES--OK

LSWC1A:	TTCALL	3,[ASCIZ "
?ONLY ONE /L ALLOWED PER FILE"]
	JRST	RESTRT

LSWCH2:	TTCALL	3,[ASCIZ "
?IMPROPER /L"]
	JRST	RESTRT

LSWCH3:	CAMN	TA,LABSIZ(DA)
	JRST	ENDSW1
	JRST	LSWC1A
;"R" SWITCH
;GET RECORD SIZE

RSWICH:	PUSHJ	PP,GETDEC	;GET DECIMAL NUMBER
	JUMPE	TA,RSWCH2
	SKIPE	RECSIZ		;RECORD SIZE PREVIOUSLY SPECIFIED?
	JRST	RSWCH1		;YES--ERROR MAYBE
	MOVEM	TA,RECSIZ	;NO--STASH THIS ONE
	JRST	ENDSW1

RSWCH1:	CAMN	TA,RECSIZ	;IS THIS SIZE SAME AS BEFORE?
	JRST	ENDSW1		;YES--OK
	TTCALL	3,[ASCIZ "
?ONLY ONE /R ALLOWED"]
	JRST	RESTRT

RSWCH2:	TTCALL	3,[ASCIZ "
?IMPROPER /R"]
	JRST	RESTRT


;"T" SWITCH

;GET A DEVICE FOR TEMP FILES

TSWICH:	PUSHJ	PP,GETNAM	;GET DEVICE NAME
	AOS	TB,NUMDEV+SRTDEF
	CAILE	TB,MAXDEV
	JRST	TSWCH2

	JUMPE	TA,TSWCH1

	MOVEM	TA,SRTDEF+DEVICE-1(TB)
	CAIE	CH,","
	JRST	ENDSW1
	JRST	TSWICH

TSWCH1:	TTCALL	3,[ASCIZ "
?IMPROPER /T"]
	JRST	RESTRT

TSWCH2:	TTCALL	3,[ASCIZ "
?TOO MANY /T DEVICES"]
	JRST	RESTRT
;"K" SWITCH
;SET UP A KEY ENTRY

KSWICH:	CAIN	KP,ENDKEY	;ANY ROOM FOR KEY?
	JRST	KSWCH9		;NO

	PUSHJ	PP,GETKAR	;GET S, U OR X
	MOVEI	TA,0
	CAIN	CH,"S"
	MOVEI	TA,SIGNED
	CAIN	CH,"U"
	MOVEI	TA,UNSIGN
	JUMPE	TA,KSWCH1

	MOVSM	TA,KTYPE(KP)	;STASH TYPE

	CAIN	TA,ALPHA	;WAS IT ALPHA?
	JRST	KSWCH2		;YES--THEREFORE NOT NUMERIC

	PUSHJ	PP,GETKAR	;GET C, N, X OR F
	MOVEI	TA,0
KSWCH1:	CAIN	CH,"C"
	MOVEI	TA,COMP
	CAIN	CH,"N"
	MOVEI	TA,DISPLY
	CAIN	CH,"F"
	MOVEI	TA,FLPT
	CAIN	CH,"X"
	MOVEI	TA,ALPHA
	JUMPN	TA,KSWC1B

	MOVEI	TA,ALPHA
	SKIPE	KTYPE(KP)
	MOVEI	TA,DISPLY
	IORI	SW,REGET

KSWC1B:	HRRM	TA,KTYPE(KP)	;STASH NUMERIC TYPE

KSWCH2:	PUSHJ	PP,GETKAR	;GET D OR A
	CAIN	CH,"A"
	JRST	KSWCH4
	MOVSI	TA,1B18
	CAIE	CH,"D"
	JRST	KSWCH3
	IORM	TA,KTYPE(KP)
	JRST	KSWCH4

KSWCH3:	CAIG	CH,"9"		;IS IT A DIGIT?
	CAIGE	CH,"0"
	JRST	KSWCHE		;NO--ERROR
	IORI	SW,REGET	;YES--SET 'REGET COMMAND CHARACTER'
;"K" SWITCH (CONT'D)

KSWCH4:	PUSHJ	PP,GETDEC	;GET LOW BYTE POSITION
	MOVEM	TA,LOWBYT(KP)

	HRRZ	TA,KTYPE(KP)	;IS DATUM
	CAIN	TA,FLPT		;  FLOATING-POINT?
	JRST	KSWCH6		;YES
	CAIN	TA,COMP		;NO--COMP?
	IORI	SW,KEYCMP	;YES--SET FLAG

	CAIE	CH,"."		;IS SEPERATOR "."?
	JRST	KSWCHE		;NO--ERROR

KSWCH5:	PUSHJ	PP,GETDEC	;GET KEY SIZE
	MOVEM	TA,KEYSIZ(KP)
	ADDI	KP,KEYENT
	SOS	NUMKEY
	JRST	KSWCH7

KSWCH6:	IORI	SW,KEYCMP	;SET FLAG
	CAIN	CH,"."
	JRST	KSWCH5

KSWCH7:	CAIN	CH,","
	JRST	KSWICH
	JRST	ENDSW1

KSWCH9:	TTCALL	3,[ASCIZ "
?TOO MANY KEYS"]
	JRST	RESTRT

KSWCHE:	TTCALL	3,[ASCIZ "
?IMPROPER /K"]
	JRST	RESTRT


;TERMINATE SWITCH LOGIC

ENDSW:	PUSHJ	PP,GETKAR	;GET NEXT COMMAND CHARACTER

ENDSW1:	MOVEI	TA,0		;CLEAR TA
	JRST	COMS3		;LOOP BACK TO COMMAND SCANNER
;GET A SIXBIT NAME FROM COMMAND STRING

GETNAM:	MOVEI	TA,0		;CLEAR RESULT
	MOVE	TB,[POINT 6,TA]	;POINT TO TA

GTNAM1:	PUSHJ	PP,GETKAR	;GET A CHARACTER
	CAIN	CH,40		;SKIP PAST
	JRST	GTNAM1		;  SPACES

GTNAM2:	CAILE	CH,"Z"		;IS IT A LETTER?
	POPJ	PP,		;NO--CAN'T BE DIGIT EITHER
	CAIL	CH,"A"
	JRST	GTNAM3		;IT IS A LETTER

	CAIG	CH,"9"		;IS IT A
	CAIGE	CH,"0"		;  DIGIT?
	POPJ	PP,		;NO

GTNAM3:	SUBI	CH,40		;CONVERT TO SIXBIT
	TLNE	TB,770000	;IS TA FULL?
	IDPB	CH,TB		;NO--STASH ANOTHER CHARACTER
	PUSHJ	PP,GETKAR	;GET NEXT CHARACTER
	JRST	GTNAM2		;LOOP

;GET A DECIMAL NUMBER FROM COMMAND STRING

GETDEC:	MOVEI	TA,0		;CLEAR RESULT

GTDEC1:	PUSHJ	PP,GETKAR	;SKIP
	CAIN	CH," "		;  PAST
	JRST	GTDEC1		;  SPACES

GTDEC2:	CAIG	CH,"9"		;DIGIT?
	CAIGE	CH,"0"
	POPJ	PP,		;NO--RETURN

	IMULI	TA,^D10		;YES--ADD
	ADDI	TA,-"0"(CH)	;  TO RESULT
	PUSHJ	PP,GETKAR	;GET NEXT CHARACTER
	JRST	GTDEC2		;LOOP
;GET AN OCTAL NUMBER FROM COMMAND STRING.
;EXIT WHEN A NON-OCTAL CHARACTER SEEN, OR MORE THAN HALF-WORD SCANNED.

GETOCT:	MOVEI	TA,0
GETOC1:	PUSHJ	PP,GETKAR
	CAIG	CH,"7"
	CAIGE	CH,"0"
	POPJ	PP,

	LSH	TA,3
	IORI	TA,-"0"(CH)
	TLNN	TA,-1
	JRST	GETOC1

	POPJ	PP,
;GET CHARACTER FROM COMMAND STRING

GETKAR:	TRZE	SW,REGET	;REGET PREVIOUS CHARACTER?
	JRST	GTKR10		;YES

	PUSHJ	PP,GTKR11

	TRNE	SW,COMEOF	;ANY MORE COMMANDS?
	JRST	GTKAR7		;NO

	CAILE	CH,137		;ABOVE SIXBIT RANGE?
	JRST	GTKAR3		;YES
	CAIGE	CH,40		;NO--BELOW SIXBIT RANGE?
	JRST	GTKAR8		;YES

	CAIE	CH,"-"		;CONTINUATION?
	JRST	GTKAR2		;NO
	PUSHJ	PP,GETKAR	;YES--NEXT BETTER BE <C.R.>
	CAIE	CH,15
	JRST	BADCON
	JRST	GETKAR

GTKAR2:	CAIN	CH,"_"		;CHANGE '_' TO '='
	MOVEI	CH,"="
	MOVEM	CH,COMKAR	;SAVE CHARACTER
	POPJ	PP,

;CHARACTER WAS ABOVE 137

GTKAR3:	CAILE	CH,"Z"+40	;LOWER
	JRST	GTKAR4		;  CASE
	CAIGE	CH,"A"+40	;  LETTER?
	JRST	GTKAR5
	SUBI	CH,40		;YES--CONVERT TO UPPER CASE
	JRST	GTKAR2

GTKAR4:	CAIE	CH,175		;IS IT ONE OF
	CAIN	CH,176		;  THE FLAVORS OF ALT-MODE?
	JRST	GTKAR6		;YES

GTKAR5:	TTCALL	3,[ASCIZ "
?IMPROPER CHARACTER IN COMMAND"]
	JRST	RESTRT

GTKAR6:	TRNN	SW,COMTTY	;INPUT COMMAND FROM TTY?
	TTCALL	3,[ASCIZ "
"]

GTKAR7:	MOVEI	CH,15
	JRST	GTKAR2
;GET CHARACTER FROM COMMAND STRING (CONT'D)

;CHARACTER WAS < 40

GTKAR8:	JUMPE	CH,GETKAR	;IGNORE NULLS
	CAIN	CH,15		;  AND
	JRST	GETKAR		;  <C.R.>

	CAILE	CH,14		;IS IT PRINTER-CONTROL?
	JRST	GTKAR9
	CAIL	CH,12
	JRST	GTKAR6		;YES

	CAIE	CH,11		;IS IT TAB?
	JRST	GTKAR5		;NO
	MOVEI	CH,40		;YES--CONVERT TO SPACE
	JRST	GTKAR2

GTKAR9:	CAIE	CH,33		;ALT-MODE?
	CAIN	CH,32		;E-O-F?
	JRST	GTKAR6		;YES

	CAIG	CH,24		;PRINTER CONTROL?
	CAIGE	CH,20
	JRST	GTKAR5		;NO
	JRST	GTKAR6		;YES
;REGET PREVIOUS CHARACTER

GTKR10:	MOVE	CH,COMKAR
	POPJ	PP,

;GET NEXT CHARACTER

GTKR11:	SOSG	COMBH+2
	JRST	GTKR13
GTKR12:	ILDB	CH,COMBH+1
	CAIE	CH,40		;IGNORE SPACES & TABS
	CAIN	CH,11
	JRST	GTKR11
	POPJ	PP,

GTKR13:	SKIPN	COMBH		;ARE WE GETTING COMMANDS FROM TMPCOR?
	JRST	GTKR14		;YES

	IN	COM,
	JRST	GTKR12

	IORI	SW,COMEOF
	GETSTS	COM,CH
	TRNE	CH,$EOF
	JRST	GTKR15

	TTCALL	3,[ASCIZ "
?ERROR READING COMMAND FILE
"]
	JRST	START
;DELETE TMPCOR AREA

GTKR14:	IORI	SW,COMEOF
	MOVSI	CH,(SIXBIT "SOR")
	MOVEM	CH,COMBH+1
	MOVE	CH,JOBFFS
	SUBI	CH,1
	HRLI	CH,-200
	MOVEM	CH,COMBH+2
	MOVE	CH,[XWD 2,COMBH+1]
	CALLI	CH,$TMPCR
	JFCL
	JRST	GTKR17


;DELETE COMMAND FILE IF EXTENSION IS "TMP"

GTKR15:	CLOSE	COM,
	HLRZ	CH,COMEXT
	CAIE	CH,(SIXBIT "TMP")
	JRST	GTKR16

	MOVEI	CH,0
	RENAME	COM,CH
	JFCL

GTKR16:	RELEASE	COM,

GTKR17:	MOVEI	CH,15
	POPJ	PP,
;SET UP INDIRECT COMMAND FILE

INDCOM:	SETZM	COMDEV		;CLEAR
	SETZM	COMFIL		;  COMMAND
	SETZM	COMEXT		;  PARAMETERS

	PUSHJ	PP,GETNAM	;GET A SIXBIT WORD
	CAIE	CH,":"
	JRST	INDC2
	MOVEM	TA,COMDEV
	PUSHJ	PP,GETNAM

INDC2:	MOVEM	TA,COMFIL
	CAIE	CH,"."
	JRST	INDC3
	PUSHJ	PP,GETNAM
	HLLZM	TA,COMEXT

INDC3:	CAIE	CH,15
	JRST	COMS9

INDC4:	SKIPN	TA,COMDEV
	MOVSI	TA,(SIXBIT "DSK")
	MOVEM	TA,COMDEV

	CALLI	TA,$DEVCH
	JUMPE	TA,BADCDV
	TLNN	TA,$INDEV
	JRST	NOTINP

	TLNN	TA,$AVAIL
	JRST	NOTAVL

	TLNE	TA,$CONSL
	JRST	INDC6

	TLNN	$DIREC
	JRST	INDC5
	SKIPN	COMFIL
	JRST	NONAME

INDC5:	TROA	SW,COMTTY

INDC6:	TRZ	SW,COMTTY
;SET UP INDIRECT COMMAND FILE (CONT'D)

INDC7:	ANDI	SW,COMTTY!KEYCMP
	MOVE	TA,JOBFF	;SAVE JOBFF
	CALLI	0		;RESET
	MOVEM	TA,JOBFF	;RESTORE JOBFF
	MOVEI	TA,1
	MOVE	TB,COMDEV
	MOVEI	TC,COMBH
	OPEN	COM,TA
	JRST	NOTAVL

	INBUF	COM,1
	MOVE	TA,JOBFF
	MOVEM	TA,TOPCOM

	TRNN	SW,COMTTY	;IS NEW DEVICE THE TTY?
	JRST	INDC11		;YES

	MOVE	TA,COMFIL
	HLLZ	TB,COMEXT
	SETZB	TC,TD
	JUMPE	TB,INDC10

INDC8:	LOOKUP	COM,TA
	JRST	NOFIND

	JRST	COMSTR

INDC10:	LOOKUP	COM,TA
	SKIPA			;COULDN'T FIND IT WITH ZERO EXTENSION
	JRST	COMSTR


	MOVSI	TB,(SIXBIT "CCL"); TRY EXTENSION OF 'CCL'
	SETZB	TC,TD
	LOOKUP	COM,TA
	JRST	NOFIND		;NO LUCK

	HLLZM	TB,COMEXT
	JRST	COMSTR

;NEW DEVICE IS TTY

INDC11:	SKIPN	OUTDEF+DEVICE	;ANY OUTPUT DEVICE YET?
	SKIPE	OUTDEF+FILNAM	;  OR FILE-NAME?
	JRST	COMSTR		;YES

	SKIPE	OUTDEF+FILEXT	;NO--ANY EXTENSION?
	JRST	COMSTR		;YES

	MOVEI	SW,0		;NO--START FROM SCRATCH
	JRST	START1
;SET UP CCL COMMAND FILE

CCLCOM:	JSP	TA,RESET
	MOVEI	SW,COMTTY

	MOVSI	TA,(SIXBIT "SOR")
	MOVEM	TA,COMBH+1
	HRRZ	TA,JOBFF
	SUBI	TA,1
	HRLI	TA,-200
	MOVEM	TA,COMBH+2

	MOVE	TA,[XWD 1,COMBH+1]
	CALLI	TA,$TMPCR
	JRST	CCLC5

	HRRZ	TB,JOBFF
	HRLI	TB,(POINT 7,0)
	MOVEM	TB,COMBH+1
	ADDM	TA,JOBFF

	IMULI	TA,5
	ADDI	TA,1
	MOVEM	TA,COMBH+2
	SETZM	COMBH

	JRST	START0


;SET UP TTY AS COMMAND DEVICE

TTYCOM:	JSP	TA,RESET	;RESET IMPURE AREA
	MOVEI	SW,0

	INIT	COM,1
	SIXBIT	"TTY"
	XWD	0,COMBH
	JRST	4,START

	INBUF	COM,1
	JRST	START0
;SET UP CCL COMMAND FILE (CONT'D)
;NO TMPCOR DATA -- FIND COMMAND FILE

CCLC5:	CALLI	TA,$PJOB
	MOVEI	TD,3
CCLC6:	IDIVI	TA,^D10
	LSHC	TB,-6
	SOJG	TD,CCLC6

	TLO	TC,(SIXBIT "000")
	HRRI	TC,(SIXBIT "SOR")
	MOVEM	TC,COMFIL

	MOVSI	TA,(SIXBIT "TMP")
	MOVEM	TA,COMEXT

	MOVSI	TB,(SIXBIT "DSK")
	MOVEI	TA,0
	MOVEI	TC,COMBH
	MOVEM	TB,COMDEV

	OPEN	COM,TA
	JRST	START

	INBUF	COM,1

	MOVE	TA,COMFIL
	MOVE	TB,COMEXT
	SETZB	TC,TD
	LOOKUP	COM,TA
	JRST	START

	JRST	START0
SUBTTL SCAN THROUGH FILE PARAMETERS CHECKING FOR ERRORS

SCANFI:	MOVEI	TD,ASCMOD	;INPUT DEFAULT MODE IS ASCII UNLESS
	TRNE	SW,KEYCMP	;  THERE ARE
	MOVEI	TD,SIXMOD	;  COMP OR FL.PT. KEYS
	SKIPGE	RECMOD+INDEF	;ANY INPUT MODE?
	MOVEM	TD,RECMOD+INDEF
	MOVEI	DA,INDEF
	JRST	SCANF

;ENTRY FOR OUTPUT FILE

SCANFO:	MOVE	TD,RECMOD+INDEF
	SKIPGE	RECMOD+OUTDEF
	MOVEM	TD,RECMOD+OUTDEF
	MOVEI	DA,OUTDEF

;ENTRY FOR BOTH INPUT AND OUTPUT

SCANF:	SKIPE	TE,NUMDEV(DA)	;ANY DEVICES?
	JRST	SCNF3		;YES
	MOVSI	TA,(SIXBIT "DSK") ;USE 'DSK'
	MOVEM	TA,DEVICE(DA)
	MOVEI	TE,1
	MOVEM	TE,NUMDEV(DA)

SCNF3:	MOVEI	TB,DEVICE(DA)	;GET READY TO LOOK AT FIRST DEVICE

SCNF4:	MOVE	TC,0(TB)	;GET NEXT DEVICE
	CALLI	TC,$DEVCH	;GET IT'S CHARACTERISTICS
	JUMPE	TC,NOTDEV	;IF ZERO--NOT DEFINED
	TLNN	TC,$AVAIL	;IS IT AVAILABLE?
	JRST	UNAVAL		;NO

	MOVEI	TD,LABELS	;IF DIRECTORY
	TLNE	TC,$DIREC	;  DEVICE,
	MOVEM	TD,LABEL(DA)	;  STANDARD LABELS

	MOVE	TD,NUMDEV(DA)	;IF MORE
	CAIE	TD,1		;  THAN ONE
	TLNE	TC,$MTA		;  DEVICE, THEY BETTER BE
	JRST	SCNF5		;  MAG-TAPES

	TTCALL	3,[ASCIZ "?MULTIPLE OUTPUT DEVICES MUST BE MAG-TAPES
"]
	JRST	SCNF10

SCNF5:	ADDI	TB,1
	SOJG	TE,SCNF4
;SET UP DEFAULT CONDITIONS

SCNF10:	MOVEI	TA,LABELO	;LABELS ARE OMITTED UNLESS
	SKIPE	FILNAM(DA)	;  THERE IS A FILE NAME,
	MOVEI	TA,LABELS	;  IN WHICH CASE THEY ARE STANDARD
	SKIPGE	LABEL(DA)
	MOVEM	TA,LABEL(DA)

	SKIPGE	BLOCKF(DA)
	SETZM	BLOCKF(DA)

;BE SURE THERE IS FILE NAME FOR STANDARD LABELLED FILES

	MOVE	TA,LABEL(DA)
	CAIN	TA,LABELS
	SKIPE	FILNAM(DA)
	JRST	SCNF15

SCNF11:	SETZM	FILEXT(DA)
	MOVEI	TA,[ASCIZ "INPUT"]
	CAIE	DA,INDEF
	MOVEI	TA,[ASCIZ "OUTPUT"]
	TRNE	SW,COMTTY	;IF COMMANDS FROM TTY, OK
	JRST	SCNF14		;  ELSE ERROR

	TTCALL	3,(TA)
	TTCALL	3,[ASCIZ " FILE NAME: "]

	PUSHJ	PP,GETNAM
	MOVEM	TA,FILNAM(DA)

	CAIE	CH,"."
	JRST	SCNF12
	PUSHJ	PP,GETNAM
	HLLZM	TA,FILEXT(DA)

SCNF12:	CAIN	CH,15
	JRST	SCNF13
	PUSHJ	PP,SKPCR
	JRST	SCNF11

SCNF13:	SKIPN	FILNAM(DA)
	JRST	SCNF11
	JRST	SCNF15

SCNF14:	TTCALL	3,[ASCIZ "?NO "]
	TTCALL	3,(TA)
	TTCALL	3,[ASCIZ " NAME SPECIFIED
"]
	JRST	RESTRT
;BE SURE RECORD AREA CAN CONTAIN LABEL

SCNF15:	SKIPN	TA,LABSIZ(DA)	;ANY LABEL SIZE?
	POPJ	PP,

	MOVEI	TC,6		;ASSUME SIXBIT
	MOVE	TB,RECMOD(DA)	;IS IT REALLY
	CAIE	TB,SIXMOD	;  SIXBIT?
	MOVEI	TC,5		;NO--FIVE BYTES PER WORD

	ADDI	TA,-1(TC)	;COMPUTE
	IDIVI	TA,(TC)		;  WORDS FOR LABEL

	CAMLE	TA,MAXLBL	;GREATER THAN BIGGEST SO FAR?
	MOVEM	TA,MAXLBL	;YES--SAVE THIS SIZE
	POPJ	PP,
SUBTTL SET UP FILE TABLES

;INPUT FILE

SETFT:	SKIPE	FILNAM+INDEF	;ANY FILE NAME?
	SKIPA	TA,[XWD INDEF+FILNAM,INFTAB]; YES--USE IT
	MOVE	TA,[XWD INFNAM,INFTAB]; NO--USE SPECIAL ONE
	BLT	TA,INFTAB+1

	MOVEI	FT,INFTAB
	MOVEI	DA,INDEF
	PUSHJ	PP,SETFT4

;OUTPUT FILE

	SKIPE	FILNAM+OUTDEF
	SKIPA	TA,[XWD OUTDEF+FILNAM,OTFTAB]
	SKIPA	TA,[XWD OTFNAM,OTFTAB]
	TDCA	FT,FT
	MOVEI	FT,1
	BLT	TA,OTFTAB+1(FT)

	MOVEI	FT,OTFTAB
	MOVEI	DA,OUTDEF
	PUSHJ	PP,SETFT4

;SORT FILE

	MOVE	TA,[XWD SRFNAM,SRFTAB]
	BLT	TA,SRFTAB+1

	MOVEI	FT,SRFTAB

	MOVE	TA,NUMDEV+SRTDEF
	CAIGE	TA,3
	MOVEI	TA,3
	MOVEM	TA,NUMDEV+SRTDEF

	MOVE	TA,RECMOD+INDEF
	CAME	TA,RECMOD+OUTDEF
	MOVEI	TA,SIXMOD
	MOVEM	TA,RECMOD+SRTDEF

	MOVSI	TA,1B<^D18+^D6>	;SET 'DONT ALLOCATE BUFFERS'
	IORM	TA,SRFTAB+^D8

	MOVEI	DA,SRTDEF
SETFT4:	MOVEI	TA,FTSIZE+FTXTRA(FT)
	CAIE	FT,SRFTAB
	DPB	TA,PTRNX

	MOVE	TA,RECLOC
	DPB	TA,PTRRA

	MOVEI	TA,DEVICE(DA)
	DPB	TA,PTRDA

	MOVE	TA,NUMDEV(DA)
	DPB	TA,PTRND

	MOVE	TA,BLOCKF(DA)
	DPB	TA,PTRBF

	MOVE	TA,RECSIZ
	DPB	TA,PTRRS

	MOVE	TA,LABSIZ(DA)
	DPB	TA,PTRLS

	MOVE	TA,LABEL(DA)
	DPB	TA,PTRLB

	MOVE	TA,RECMOD(DA)
	DPB	TA,PTRDM

	MOVE	TA,RECMOD+INDEF
	CAME	TA,RECMOD+OUTDEF
	MOVEI	TA,SIXMOD
	CAIN	TA,SIXMOD
	TDCA	TA,TA
	MOVEI	TA,1
	DPB	TA,PTRCM

	MOVEI	TA,3
	DPB	TA,PTRVER

	MOVEI	TA,PPN(DA)
	DPB	TA,PTRPP

	SKIPN	FILNAM(DA)
	POPJ	PP,

	MOVEI	TB,FILNAM(DA)
	HRLI	TB,(POINT 6,0)
	MOVEM	TB,PTRFN(FT)

	POPJ	PP,
SUBTTL BUILD 'KEY SETUP' ROUTINE

KEYBLD:	MOVEI	KP,KEYDEF	;INITIALIZE
	HRL	KP,NUMKEY	;  KP
	MOVE	TA,KEYWRK	;  AND
	MOVEM	TA,KEYLOC	;  KEYLOC

KEYB1:	HRRZ	TA,KTYPE(KP)	;GET KEY TYPE
	CAILE	TA,NTYPES	;IF FUNNY,
	MOVEI	TA,0		;  RESET
	PUSHJ	PP,@KEYB2(TA)	;GO TO ONE OF THE ROUTINES

	ADDI	KP,KEYENT-1	;BUMP TO
	AOBJN	KP,KEYB1	;  NEXT ENTRY AND LOOP

	MOVSI	TA,(POPJ 17,)	;NO MORE--
	JRST	PUTKYW		;PUT OUT EXIT AND RETURN

KEYB2:	EXP	KEYB30		;UNDEFINED
	EXP	KEYB4		;COMP
	EXP	KEYB15		;NUMERIC DISPLAY
	EXP	KEYB20		;FLOATING-POINT
	EXP	KEYB3		;ALPHANUMERIC
;ALPHANUMERIC KEY

KEYB3:	MOVE	TA,[PUSHJ 17,KEY.]
	PUSHJ	PP,PUTKYW

	MOVE	TA,RECMOD+SRTDEF
	MOVEI	TE,6
	CAIE	TA,SIXMOD
	MOVEI	TE,5

	MOVE	TA,LOWBYT(KP)
	SUBI	TA,1
	IDIVI	TA,0(TE)
	ADD	TA,RECLOC

	HRLI	TA,(POINT 6,0)
	CAIE	TE,6
	HRLI	TA,(POINT 7,0)
	LDB	TD,[POINT 6,TA,11]
	IMULI	TA+1,(TD)
	MOVEI	TD,^D36
	SUBI	TD,(TA+1)
	DPB	TD,[POINT 6,TA,5]
	PUSHJ	PP,PUTKYW

	HRLZ	TA,KEYSIZ(KP)
	HRR	TA,KEYLOC
	SKIPL	KTYPE(KP)	;DESCENDING?
	TLZA	TA,1B18		;NO--CLEAR BIT
	TLO	TA,1B18		;YES--SET BIT
	PUSHJ	PP,PUTKYW

	MOVE	TA,KEYSIZ(KP)
	ADDI	TA,4
	IDIVI	TA,5
	ADDM	TA,KEYLOC
	POPJ	PP,
;COMPUTATIONAL

KEYB4:	MOVE	TA,LOWBYT(KP)
	SUBI	TA,1
	IDIVI	TA,6
	ADD	TA,RECLOC

	LDB	TC,[POINT 17,KTYPE(KP),17]

	MOVE	TB,KEYSIZ(KP)
	CAILE	TB,^D10
	JRST	KEYB8

KEYB5:	HRLI	TA,(MOVE)
	CAIN	TC,UNSIGN
	HRLI	TA,(MOVM)

KEYB6:	PUSHJ	PP,PUTKYW

KEYB7:	MOVSI	TA,(SETCA)
	SKIPGE	KTYPE(KP)
	PUSHJ	PP,PUTKYW

	MOVSI	TA,(MOVEM)
	HRR	TA,KEYLOC
	AOS	KEYLOC
	JRST	PUTKYW

;TWO-WORD COMP

KEYB8:	CAIN	TC,UNSIGN
	JRST	KEYB9

	MOVE	TC,TA
	PUSHJ	PP,KEYB5
	MOVEI	TA,1(TC)
	JRST	KEYB5

;UNSIGNED TWO-WORD COMP

KEYB9:	HRLI	TA,(UMAG)
KEYB10:	PUSHJ	PP,KEYB6

	MOVSI	TA,(SETCA 1,)
	SKIPGE	KTYPE(KP)
	PUSHJ	PP,PUTKYW

	MOVSI	TA,(MOVEM 1,)
	HRR	TA,KEYLOC
	AOS	KEYLOC
	JRST	PUTKYW
;NUMERIC DISPLAY

KEYB15:	MOVSI	TA,(UGD6)
	MOVE	TB,SRTDEF+RECMOD
	CAIE	TB,SIXMOD
	MOVSI	TA,(UGD7)
	HRR	TA,JOBFF
	ADDI	TA,2
	PUSHJ	PP,PUTKYW

	MOVSI	TA,(SKIPA)
	PUSHJ	PP,PUTKYW

	MOVE	TA,LOWBYT(KP)
	SUBI	TA,1

	MOVE	TE,SRTDEF+RECMOD
	MOVEI	TD,6
	CAIE	TE,SIXMOD
	MOVEI	TD,5
	IDIVI	TA,(TD)
	ADD	TA,RECLOC

	CAIE	TD,6
	MOVEI	TD,7
	IMULI	TD,(TA+1)
	MOVNS	TD
	ADDI	TD,^D36
	DPB	TD,[POINT 6,TA,5]

	MOVE	TB,KEYSIZ(KP)
	DPB	TB,[POINT 11,TA,17]

	LDB	TB,[POINT 17,KTYPE(KP),17]
	CAIN	TB,UNSIGN
	TLZA	TA,1B<^D18+6>
	TLO	TA,1B<^D18+6>

	MOVE	TB,KEYSIZ(KP)
	CAILE	TB,^D10
	JRST	KEYB10
	JRST	KEYB6

;FLOATING-POINT

KEYB20:	SETZM	KEYSIZ(KP)
	JRST	KEYB4

;BAD CODE

KEYB30:	TTCALL	3,[ASCIZ "?PROGRAM ERROR--BAD KEY TYPE
"]
	CALLI	$EXIT
SUBTTL SCAN THROUGH KEY PARAMETERS LOOKING FOR ERRORS

SCNKEY:	MOVEI	KP,KEYDEF	;START AT TOP
	HRL	KP,NUMKEY	;BUILD IOWD
	TLNN	KP,-1		;ANY KEYS?
	JRST	NOKEYS		;NO--ERROR

SCNK1:	SKIPE	TA,LOWBYT(KP)	;ANY LOW BYTE?
	CAMLE	TA,RECSIZ	;YES--INSIDE RECORD?
	JRST	OUTRNG		;YES--ERROR

	HRRZ	TB,KTYPE(KP)	;IS DATUM FLOATING-POINT?
	CAIN	TB,FLPT
	JRST	SCNK2		;YES

	SKIPN	TC,KEYSIZ(KP)	;ANY SIZE?
	JRST	ZEROSZ		;NO--ERROR

	CAIE	TB,COMP		;IS DATUM COMP?
	JRST	SCNK4		;NO--MUST BE DISPLAY

SCNK2:	MOVE	TE,INDEF+RECMOD	;IS RECORD MODE SIXBIT?
	MOVE	TD,OUTDEF+RECMOD
	CAIE	TE,ASCMOD
	CAIN	TD,ASCMOD
	JRST	NOTASC		;NO--TROUBLE

	MOVE	TC,LOWBYT(KP)	;IS FIELD
	IDIVI	TC,6		;  IN CORRECT
	CAIE	TC+1,1		;  PLACE?
	JRST	BADLOW		;NO--ERROR

	MOVEI	TC,^D12		;ASSUME TWO-WORD COMP
	MOVE	TD,KEYSIZ(KP)
	CAIE	TB,FLPT
	CAIG	TD,^D10
	MOVEI	TC,^D6

SCNK4:	ADDI	TC,-1(TA)	;IS KEY
	CAMLE	TC,RECSIZ	;  FULLY WITHIN RECORD?
	JRST	OUTRNG		;NO--ERROR

	HRRZ	TA,KTYPE(KP)	;ADD TO
	CAIG	TA,NTYPES	;  SIZE OF
	PUSHJ	PP,@SCNK10(TA)	;  KEY AREA

SCNK9:	ADDI	KP,KEYENT-1	;BUMP UP TO
	AOBJN	KP,SCNK1	;  NEXT KEY
	POPJ	PP,		;NO MORE--QUIT
;INCREMENT SIZE OF AREA TO HOLD KEYS

SCNK10:	EXP	SCNK14		;UNDEFINED
	EXP	SCNK11		;COMP
	EXP	SCNK11		;NUMERIC DISPLAY
	EXP	SCNK12		;FLOATING-POINT
	EXP	SCNK13		;ALPHANUMERIC

SCNK11:	MOVE	TA,KEYSIZ(KP)	;IS ITEM ONE WORD
	CAILE	TA,^D10		;  OR TWO?
	AOS	KEYWRK		;TWO
SCNK12:	AOS	KEYWRK		;ONE
	POPJ	PP,

SCNK13:	MOVE	TA,KEYSIZ(KP)	;ENOUGH WORDS
	ADDI	TA,4		;  FOR
	IDIVI	TA,5		;  FIVE BYTES
	ADDM	TA,KEYWRK	;  PER WORD
SCNK14:	POPJ	PP,
SUBTTL MISCELLANEOUS ROUTINES

;RESET PROGRAM

RESET:	HRRZM	TA,RETLOC	;SAVE RETURN ADDRESS

	SETZM	FILES.##		;DON'T LET RESET SET UP FILES
	MOVE	TA,JOBSA	;SAVE JOBSA SO
	MOVEM	TA,JOBSAS	;  WE CAN RESET LATER
	JSP	14,RESET.
	EXP	FAKNAM
	MOVE	TA,JOBSAS	;RESET JOBSA
	MOVEM	TA,JOBSA
	MOVEI	TA,INFTAB	;RESET FILES.
	MOVEM	TA,FILES.

	MOVSI	TA,177770	;SET 'OPEN CHANNELS' WORD
	MOVEM	TA,OPNCH.

	SETZM	PRGFLG##	;IN CASE LEFT SET ON A "CNT-C, START"

	MOVEI	CH,15
	MOVEM	CH,COMKAR
	MOVE	TA,JOBFF
	MOVEM	TA,JOBFFS

	MOVE	TA,[XWD SETWRT,WRITE]
	BLT	TA,CALLST+3
	MOVE	TA,[PUSHJ 17,UUO.]
	MOVEM	TA,JOB41

	JRST	@RETLOC		;RETURN

SETWRT:	EXP	OTFTAB
	EXP	1B13+10B17+1B35
	POPJ	PP,

	TTCALL	3,[ASCIZ "?INVALID KEY ON SORT OUTPUT
"]
	CALLI	$EXIT

	PUSHJ	17,PSORT.
	EXP	SRFTAB
	Z
	POPJ	PP,

FAKNAM:	Z			;FAKE ARGS FOR RSTLNK
	FAKNAM,,FILES.
;STASH A SINGLE 'KEY BUILDER' WORD

PUTKYW:	MOVEM	TA,@JOBFF
	AOSA	TA,JOBFF

;INCREMENT JOBFF

SETJFF:	ADDB	TA,JOBFF

	IORI	TA,1777		;ENOUGH ROOM IN CORE?
	CAMG	TA,JOBREL
	POPJ	PP,		;YES

	CALLI	TA,$CORE	;NO--GET MORE
	SKIPA			;ERROR
	POPJ	PP,

	TTCALL	3,[ASCIZ "?NOT ENOUGH CORE TO SET UP SORT
"]
	JRST	RESTRT
SUBTTL ERROR ROUTINES

;INDIRECT COMMAND DEVICE DOESN'T EXIST

BADCDV:	MOVEI	TB,COMDEV
	PUSHJ	PP,TYPDEV
	TTCALL	3,[ASCIZ " DOESN'T EXIST"]
	JRST	START
;NO FILE NAME FOR INDIRECT COMMAND FILE

NONAME:	TTCALL	3,[ASCIZ "
?INDIRECT COMMAND FILE NEEDS FILE NAME"]
	JRST	START

;INDIRECT COMMAND DEVICE NOT AVAILABLE

NOTAVL:	MOVEI	TB,COMDEV
	PUSHJ	PP,TYPDEV
	TTCALL	3,[ASCIZ " NOT AVAILABLE"]
	JRST	START

;COMMAND DEVICE IS NOT AN INPUT DEVICE

NOTINP:	MOVEI	TB,COMDEV
	PUSHJ	PP,TYPDEV
	TTCALL	3,[ASCIZ " IS NOT AN INPUT DEVICE"]
	JRST	START

;CANNOT FINE COMMAND FILE

NOFIND:	TTCALL	3,[ASCIZ "
?CANNOT FIND "]
	MOVE	TC,[POINT 6,COMDEV]
	PUSHJ	PP,PUTSIX
	TTCALL	3,[ASCIZ ":"]
	MOVE	TC,[POINT 6,COMFIL]
	PUSHJ	PP,PUTSIX
	HLLZ	TA,COMEXT
	JUMPE	TA,START
	MOVE	TC,[POINT 6,TA]
	PUSHJ	PP,PUTSIX
	JRST	START
;TYPE OUT 'TA' AS DECIMAL NUMBER

TYPDEC:	JUMPGE	TA,TYPDC1
	TTCALL	3,[ASCIZ "-"]
	MOVMS	TA
	TLZ	TA,(1B0)

TYPDC1:	IDIVI	TA,^D10
	HRLM	TB,(PP)
	SKIPE	TA
	PUSHJ	PP,TYPDC1
	HLRZ	TA,(PP)
	ADDI	TA,"0"
	TTCALL	1,TA
	POPJ	PP,

;TYPE OUT A WORD IN SIXBIT

PUTSIX:	ILDB	CH,TC
	JUMPE	CH,PUTSX1
	ADDI	CH,40
	TTCALL	1,CH
	TLNE	TC,770000
	JRST	PUTSIX
PUTSX1:	POPJ	PP,
;NO KEYS

NOKEYS:	TTCALL	3,[ASCIZ "?NO KEYS SPECIFIED
"]
	JRST	RESTRT

;COMP OR FL.PT. KEY DOESN'T START AT NEW WORD

BADLOW:	PUSHJ	PP,TYPKEY
	TTCALL	3,[ASCIZ " -- COMP OR FLOATING-POINT KEYS MUST START A NEW WORD
"]
	JRST	SCNK9

;KEY OUT OF RANGE

OUTRNG:	PUSHJ	PP,TYPKEY
	TTCALL	3,[ASCIZ " IS OUTSIDE RECORD
"]
	JRST	SCNK9

;ZERO KEY LENGTH

ZEROSZ:	PUSHJ	PP,TYPKEY
	TTCALL	3,[ASCIZ " HAS ZERO LENGTH
"]
	JRST	SCNK9

;COMP OR FLOATING-POINT FIELDS IN ASCII RECORDS

NOTASC:	PUSHJ	PP,TYPKEY
	TTCALL	3,[ASCIZ " -- CANNOT HAVE NON-DISPLAY KEYS IN ASCII FILES
"]
	JRST	SCNK9

;TYPE OUT "?KEY M.N"

TYPKEY:	IORI	SW,COMERA	;SET ERROR FLAG
	TTCALL	3,[ASCIZ "?KEY "]
	MOVE	TA,LOWBYT(KP)
	PUSHJ	PP,TYPK1
	TTCALL	3,[ASCIZ "."]
	MOVE	TA,KEYSIZ(KP)

TYPK1:	IDIVI	TA,^D10
	HRLM	TA+1,(PP)
	SKIPE	TA
	PUSHJ	PP,TYPK1
	HLRZ	CH,(PP)
	ADDI	CH,"0"
	TTCALL	1,CH
	POPJ	PP,
;DEVICE UNAVAILABLE

UNAVAL:	PUSHJ	PP,TYPDEV
	TTCALL	3,[ASCIZ " UNAVAILABLE
"]
	JRST	SCNF5

;DEVICE DOESN'T EXIST

NOTDEV:	PUSHJ	PP,TYPDEV
	TTCALL	3,[ASCIZ " DOESN'T EXIST
"]
	JRST	SCNF5

;TYPE OUT "?DEVICE XXX"]

TYPDEV:	IORI	SW,COMERA
	MOVE	TC,[POINT 6,0(TB)]
	TTCALL	3,[ASCIZ "?DEVICE "]
	JRST	PUTSIX

;HYPHEN NOT AT END OF COMMAND LINE

BADCON:	TTCALL	3,[ASCIZ "
?CONTINUATION '-' MUST BE AT END OF LINE"]
	JRST	RESTRT

;OCUNT NOT = ICOUNT AFTER SORT COMPLETE

BADCNT:	TTCALL	3,[ASCIZ "? READ "]
	PUSHJ	PP,TYPDEC
	TTCALL	3,[ASCIZ " RECORDS, BUT WROTE "]
	MOVE	TA,OCOUNT
	PUSHJ	PP,TYPDEC
	TTCALL	3,[ASCIZ "
"]
	JRST	OPROC5
SUBTTL CONSTANTS

EXTERNAL JOBSA		;PROGRAM START ADDRESS
EXTERNAL JOBFF		;FIRST FREE LOCATION
EXTERNAL JOBREL		;HIGHEST LOCATION IN LOW-SEG
EXTERNAL JOBHRL		;HIGHEST LOC IN HI-SEG
EXTERNAL JOB41		;UUO TRAP LOCATION
EXTERNAL JOBAPR		;FLAGS FOR APR TRAPS
EXTERNAL OPNCH.		;FLAG WORD FOR AVAILABLE I/O CHANNELS

COM==1		;CHANNEL FOR COMMAND SCANNER

;SWITCH FLAGS

REGET==1B18	;REGET PREVIOUS COMMAND CHARACTER
COMEOF==1B19	;END-FILE ON COMMAND STRING
COMTTY==1B20	;COMMANDS NOT FROM TTY
COMERA==1B21	;ERROR IN COMMAND STRING
KEYCMP==1B22	;AT LEAST ONE KEY IS COMP OR FL.PT.

;'CALLI' CODES

	$RESET==0	;RESET
$EXIT==12	;EXIT
$PJOB==30	;PJOB
$CORE==11	;CORE
$DEVCH==4	;DEVCHR
$TMPCR==44	;TMPCOR
$TRAP==16	;APREN

;ACCUMULATORS

SW=0		;SWITCHES
TA=1		;TEMP
TB=2		;TEMP
TC=3		;TEMP
TD=4		;TEMP
TE=5		;TEMP
FT=12		;ADDRESS OF A FILE TABLE
CH=13		;COMMAND CHARACTER
KP=14		;POINTER TO KEY DEFINITIONS
DA=15		;POINTER TO FILE DEFINITIONS
DP=16		;POINTER TO DEVICES
PP=17		;PUSH-DOWN POINTER
;COMMUNICATION WITH MONITOR

$EOF==20000	;END-FILE FLAG IN STATUS WORD

$CONSL==1B<^D18+^D5>	;DEVICE IS A CONSOLE
$AVAIL==1B<^D18+^D12>	;DEVICE IS AVAILABLE
$MTA==1B<^D18+^D13>	;DEVICE IS A MAG-TAPE
$DIREC==1B<^D18+^D15>	;DEVICE HAS DIRECTORY
$INDEV==1B<^D18+^D16>	;DEVICE CAN DO INPUT
$OTDEV==1B<^D18+^D17>	;DEVICE CAN DO OUTPUT

;POINTERS TO FIELDS IN FILE TABLES

PTRND:	POINT  6,5(FT),17	;NUMBER OF DEVICES
PTRDA:	POINT 18,5(FT),35	;ADDRESS OF FIRST DEVICE
PTRBF:	POINT  7,6(FT),11	;BLOCKING FACTOR
PTRRS:	POINT 12,7(FT),17	;SIZE OF DATA RECORD
PTRLS:	POINT 17,^D9(FT),17	;SIZE OF LABEL RECORD
PTRDM:	POINT  2,^D8(FT),1	;DEVICE MODE
PTRCM:	POINT  1,^D8(FT),14	;CORE MODE
PTRLB:	POINT  2,^D8(FT),3	;LABEL CODE
PTRRA:	POINT 17,^D8(FT),35	;ADDRESS OF RECORD AREA
PTRNX:	POINT 18,6(FT),35	;ADDRESS OF NEXT FILE TABLE
PTRPP:	POINT 18,^D18(FT),35	;ADDRESS OF PROJ-PROG NUMBER
PTRVER:	POINT  6,5(FT),5	;VERSION NUMBER (ALWAYS 3)
PTRFN==13			;LOCATION OF VALUE-OF-ID POINTER

;PRESET PARAMETERS FOR SORT FILE

SRPAR:	Z		;FILE NAME
	Z		;EXTENSION
	Z		;BLOCKING FACTOR
	Z		;MODE
	EXP LABELS	;LABEL CODE
	Z		;LABEL SIZE
	Z		;NUMBER OF DEVICES
	Z		;PROJECT-PROGRAMMER NUMBER
	SIXBIT "DSK"
	SIXBIT "DSK"
	SIXBIT "DSK"
	SIXBIT	"DSK"
	SIXBIT	"DSK"
	SIXBIT	"DSK"

;NAMES OF FILES

OTFNAM:	SIXBIT	"SORTED-OUTPUT"
INFNAM:	SIXBIT	"SORT-INPUT"
SRFNAM:	SIXBIT	"SORT-FILE"
SUBTTL WORK LOCATIONS

	TOPHI==.	;REMEMBER HI-SEG BREAK

	RELOC	0

RETLOC:	BLOCK 1		;RETURN LOC FOR RESET
JOBFFS:	BLOCK 1		;JOBFF AFTER RESET. CALLED

LOLOW==.		;FIRST LOCATION ZEROED UPON STARTING

;FILE DEFINITIONS

COMSIZ==^D14	;SIZE OF EACH FILE PARAMETER ENTRY

INDEF:	BLOCK COMSIZ
OUTDEF:	BLOCK COMSIZ
SRTDEF:	BLOCK COMSIZ

FILNAM==0	;RELATIVE LOCATION OF FILE NAME
FILEXT==1	;    "       "        EXTENSION
BLOCKF==2	;    "       "        BLOCKING FACTOR
RECMOD==3	;    "       "        MODE
LABEL==4	;    "       "        LABEL CODE
LABSIZ==5	;    "       "        LABEL SIZE
PPN==6		;    "       "        PROJ-PROG NUMBER
NUMDEV==7	;    "       "        NUMBER OF DEVICES
DEVICE==10	;    "       "        FIRST DEVICE NAME

ASCMOD==2	;ASCII RECORDING MODE
SIXMOD==0	;SIXBIT RECORDING MODE

LABELS==1	;LABELS STANDARD
LABELN==2	;LABELS NON-STANDARD
LABELO==0	;OMITTED LABEL CODE

MAXDEV==6	;MAXIMUM NUMBER OF DEVICES ALLOWED

STDEVS==SRTDEF+DEVICE	;LOCATION OF FIRST SORT DEVICE
;AREA TO STORE KEY DEFINITIONS

KEYENT==3		;SIZE OF EACH ENTRY

KEYDEF:	BLOCK ^D20*KEYENT

ENDKEY==.		;FIRST LOCATION NOT IN TABLE

NUMKEY:	BLOCK 1		;NUMBER OF KEYS (NEGATIVE)

LOWBYT==0	;RELATIVE LOCATION OF LOW BYTE NUMBER
KEYSIZ==1	;    "       "        KEY SIZE
KTYPE==2	;    "       "        KEY TYPE CODES

SIGNED==1	;KEY IS SIGNED
UNSIGN==2	;KEY IS UNSIGNED

COMP==1		;KEY IS COMPUTATIONAL
DISPLY==2	;KEY IS DISPLAY
FLPT==3		;KEY IS FLOATING-POINT
ALPHA==4	;KEY IS ALPHANUMERIC

NTYPES==4	;NUMBER OF KEY TYPES
;MISCELLANEOUS

ICOUNT:	BLOCK 1		;COUNT OF NUMBER OF RECORDS READ
OCOUNT:	BLOCK 1		;COUNT OF NUMBER OF RECORDS WRITTEN
RECSIZ:	BLOCK 1		;RECORD SIZE IN BYTES
RECWDS:	BLOCK 1		;RECORD SIZE IN WORDS
MAXLBL:	BLOCK 1		;LARGEST NON-STANDARD LABEL
KEYWRK:	BLOCK 1		;AT FIRST, SIZE OF KEY AREA, THEN START OF KEY AREA
KEYLOC:	BLOCK 1		;LOCATION FOR NEXT KEY (SEE BLDKEY)

FTSIZE==^D25		;SIZE OF A FILE TABLE
FTXTRA==^D21		;NUMBER OF EXTRA WORDS WITH EACH FILE TABLE

	BLOCK FTXTRA	;TABLE FOR LIBOL FOR INPUT-FILE
INFTAB:	BLOCK FTSIZE	;INPUT FILE TABLE
	BLOCK FTXTRA	;LIKEWISE FOR
OTFTAB:	BLOCK FTSIZE	;  OUTPUT-FILE
	BLOCK FTXTRA	;LIKEWISE FOR
SRFTAB:	BLOCK FTSIZE	;  SORT-FILE

HILOW==.-1		;LAST LOCATION ZEROED UPON RESTART

RECLOC:	BLOCK 1		;ADDRESS OF RECORD AREA
WRITE:	BLOCK 5		;WRITE ROUTINE (SEE RESET)
CALLST:	BLOCK 4		;CALL TO PSORT. (SEE RESET)
JOBSAS:	BLOCK 1		;SAVE JOBSA DURING RESET.

COMKAR:	BLOCK	1	;LAST COMMAND CHARACTER SEEN
COMBH:	BLOCK 3		;BUFFER DATA FOR COMMAND FILE
TOPCOM:	BLOCK 1		;FIRST LOCATION ABOVE COMMAND BUFFER
COMDEV:	BLOCK 1		;COMMAND DEVICE NAME
COMFIL:	BLOCK 1		;COMMAND FILE NAME
COMEXT:	BLOCK 1		;COMMAND FILE EXTENSION

	RELOC	TOPHI	;RELOC SO LITERALS GO IN HI-SEGMENT

	END	START
