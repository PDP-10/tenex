	TITLE PINDEX

;PINDEX IS A PROGRAM WHICH READS A PARRY DATA FILE AND CREATES A RANDOM-ACCESS INDEX
;FOR THIS FILE.
;A NEW INDEX ENTRY IS GENERATED FOR EACH OCCURRENCE OF A SPECIAL CHARACTER IN
;THE INPUT FILE. THIS SPECIAL CHARACTER IS CURRENTLY # AND IS ASSUMED TO OCCUR
;ONLY IN THE FOLLOWING CONTEXT: (#X KKKKK ...
;KKKKK BECOMES THE INDEX ENTRY KEY. THE VALUE ASSOCIATED WITH THIS KEY IS THE
;CHARACTER NUMBER, N, OF THE IMMEDIATELY PRECEDING (, AS COUNTED FROM THE BEGINNING
;OF THE FILE. THUS, THE FILE RECORD NUMBER OF THE INDEX ENTRY IS N/1200
;(1200 OCTAL), AND THE CHARACTER NUMBER WITHIN THIS RECORD IS N MOD 1200.
;THE OUTPUT INDEX FILE HAS THE FORM
;
;		(KKKKK . NNNNNNNNNNNN)
;		 .
;		 .
;		 .
;		NIL
;
;WHERE KKKKK IS THE KEY AND NNNNNNNNNNNN IS THE OCTAL CHARACTER NUMBER.

P_ 17
CHR_ 16
COUNT_ 15
CHNUM_ 14
A_ 13
B_ 12

INFIL:	SIXBIT	/PDAT/			;PARRY DATA FILE
	0
	0
	0
OUTFIL:	SIXBIT	/PDATX/			;INDEX OUTPUT FILE
	0
	0
	0
OBUF:	BLOCK	3
IBUF:	BLOCK	3
PDL:	BLOCK	5


BEGIN:
	MOVE	P,[IOWD 5,PDL]
	INIT	1,0			;PREPARE INPUT FILE
	SIXBIT	/DSK/
	IBUF
	JRST	ERR1
	LOOKUP	1,INFIL
	JRST	ERR2
	INIT	2,0			;PREPARE OUTPUT FILE
	SIXBIT	/DSK/
	XWD	OBUF,0
	JRST	ERR1
	ENTER	2,OUTFIL
	JRST	ERR3
	SETOM	COUNT			;COUNT IS FILE CHARACTER COUNTER. INIT TO -1
BEG1:
	PUSHJ	P,READ			;SCAN FOR NEXT SPECIAL CHARACTER
	CAIN	CHR,"#"			;# IS THE SPECIAL CHARACTER
	PUSHJ	P,MARK			;SPECIAL CHAR FOUND. GENERATE AN INDEX ENTRY
	JRST	BEG1			;RESUME SCAN



READ:
	SOSG	IBUF+2			;GET NEXT INPUT CHARACTER
	IN	1,
	JRST	[ILDB CHR,IBUF+1ADDI COUNT,1POPJ P,]
	STATO	1,20000			;TEST FOR END OF FILE
	JRST	ERR4			;SOME FUNNY INPUT ERROR
	MOVEI	CHR,"N"			;END INDEX FILE WITH NIL
	PUSHJ	P,WRITE
	MOVEI	CHR,"I"
	PUSHJ	P,WRITE
	MOVEI	CHR,"L"
	PUSHJ	P,WRITE
	MOVEI	CHR,15
	PUSHJ	P,WRITE
	MOVEI	CHR,12
	PUSHJ	P,WRITE
	CALL	[SIXBIT /EXIT/]		;TERMINATE



WRITE:
	SOSG	OBUF+2			;WRITE A CHARACTER TO OUTPUT FILE
	OUT	2,
	JRST	[IDPB CHR,OBUF+1POPJ P,]
	JRST	ERR5			;SOME FUNNY OUTPUT ERROR



MARK:
	MOVEI	CHNUM,-1(COUNT)		;CHNUM HAS CHAR COUNT OF ENRTY BEGINNING
	MOVEI	CHR,"("			;GENERATE AN INDEX ENTRY
	PUSHJ	P,WRITE
	PUSHJ	P,READ			;FORM IS (#X NNNN ... GET NNNN
	PUSHJ	P,READ
	MOVSI	A,-5
	PUSHJ	P,READ
	PUSHJ	P,WRITE
	AOBJN	A,.-2
	MOVEI	CHR," "
	PUSHJ	P,WRITE
	MOVEI	CHR,"."
	PUSHJ	P,WRITE
	MOVEI	CHR," "
	PUSHJ	P,WRITE
	MOVE	A,[POINT 3,CHNUM]	;WRITE OUT OCTAL CHAR NUMBER OF THIS ENTRY
	MOVSI	B,-14
	ILDB	CHR,A
	ADDI	CHR,"0"
	PUSHJ	P,WRITE
	AOBJN	B,.-3
	MOVEI	CHR,")"			;FINISH OFF INDEX ENTRY
	PUSHJ	P,WRITE
	MOVEI	CHR,15
	PUSHJ	P,WRITE
	MOVEI	CHR,12
	PUSHJ	P,WRITE
	POPJ	P,			;RETURN TO SCANNER



ERR1:	OUTSTR	[ASCIZ /FILE INITIALIZATION FAILURE.
/]
	CALL	[SIXBIT/EXIT/]
ERR2:	OUTSTR	[ASCIZ /INPUT FILE LOOKUP FAILURE.
/]
	CALL	[SIXBIT/EXIT/]
ERR3:	OUTSTR	[ASCIZ /OUTPUT FILE ENTRY FAILURE.
/]
	CALL	[SIXBIT/EXIT/]
ERR4:	OUTSTR	[ASCIZ /INPUT FILE READ FAILURE.
/]
	CALL	[SIXBIT/EXIT/]
ERR5:	OUTSTR	[ASCIZ /OUTPUT FILE WRITE FAILURE.
/]
	CALL	[SIXBIT/EXIT/]

END	BEGIN
