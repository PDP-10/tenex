	title	labels
	subttl	print labels for tapes
	search	monsym

opdef	call	[pushj	17,]
opdef	ret	[popj	17,]
a=1
b=2
c=3
d=4
p=17
lpdl==20

define typem (ptr),<
	push p,a
	hrroi a,ptr
	psout
	pop p,a
>
define type (msg),<
	push p,a
	hrroi a,[asciz/msg/]
	psout
	pop p,a
>
define error (ac,retry<0>),<
	jrst   [type <
>
		move 2,ac
		movei 1,101
		hrli 2,.fhslf
		setz c,
		erstr
		 jfcl
		 jfcl
		type <
>
ifdif <retry><0>,<
		jrst retry]
>
ifidn <retry><0>,<
		 haltf]
>
>		
start:	reset
	move p,[iowd lpdl,pdl]
	call init
	call label
	call finish
	haltf
	jrst start
init:	type <How many labels? >
	movei 1,100
	movei 3,^d10
	nin
	 error 3,init
	movem 2,lcnt
init1:	type <Output to file: >
	movsi 1,3
	move 2,[100,,101]
	gtjfn
	 error 1,init1
	movem 1,ljfn
	move 2,[70000,,100000]
	openf
	 error 1
	ret
label:	type	<
Text of label is:
>	
	typem ltext
	call chktty			;if not tty, ret +2
	 jrst  [type <
Place forms in printer and carriage return>
		movei 1,101
		cfibf
l1:		pbin
		caie 1,15
		 jrst l1
		movei 1,101
		cfibf
		jrst .+1]
	call plabl
	ret

chktty:	movei 1,101
	dvchr
	ldb a,[point 9,2,17]
	caie a,12
	 aos 0(p)
	ret

plabl:	move 4,lcnt
	move 1,ljfn
	movei 3,llen
plabl1:	hrroi 2,ltext
	sout
	sojn 4,plabl1
	ret
finish:	move 1,ljfn
	closf
	 error 1,<.+1>
	type <
Done!>
	ret
pdl:  block lpdl
ljfn: block 1
lcnt: block 1
ltext: asciz/
        Sumex

Type of Tape	  Date

		  Time
--------------------------------
Sequence	Tape     of




/
llen==.-ltext
llen==llen*5
	end	start
